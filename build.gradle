plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    group 'net.leonardo_dgs'
    version '1.1.0'
    description 'Create lifts with signs!'

    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://jitpack.io/' }
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
    }

    processResources {
        expand(
                version: project.version,
                description: project.description,
                author: 'Leonardo_DGS',
                website: 'https://www.spigotmc.org/resources/71207/'
        )

        jar.enabled = false
    }

}

subprojects {
    if (it.name != 'common') {
        dependencies {
            implementation project(':common')
            implementation project(path: ':common', configuration: 'shadow')
        }

        shadowJar {
            minimize()
            relocate 'org.bstats', 'net.leonardo_dgs.signselevators.lib.bstats'
            relocate 'net.kyori', 'net.leonardo_dgs.signselevators.lib'
            relocate 'org.intellij', 'net.leonardo_dgs.signselevators.lib.intellij'
            relocate 'org.jetbrains', 'net.leonardo_dgs.signselevators.lib.jetbrains'
        }
    }
}

dependencies {
    implementation project(path: ':bukkit', configuration: 'shadow')
}

shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set('')
    archiveVersion.set('')
}

artifacts {
    archives shadowJar
}
