plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group = 'net.leonardo_dgs'
version = '1.0.0'
description = 'Create lifts with signs!'

def mainPackage = 'net.leonardo_dgs.signselevators'

tasks.compileJava.sourceCompatibility = JavaVersion.VERSION_1_8
tasks.compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-chat:1.17-R0.4'
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
}

processResources {
    expand(
            version: project.version,
            description: project.description,
            author: 'Leonardo_DGS',
            website: 'https://www.spigotmc.org/resources/71207/'
    )
}

tasks.jar.enabled = false

tasks.shadowJar {
    archiveBaseName.set(project.name)
    archiveClassifier.set("")
    archiveVersion.set("")
    minimize()

    relocate 'org.bstats.bukkit', mainPackage + '.bstats'
}

artifacts {
    archives shadowJar
}
