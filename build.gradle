import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group = 'net.leomixer17'
version = '1.0.0'
description = 'Create lifts with signs!'

tasks.compileJava.sourceCompatibility = 1.8
tasks.compileJava.targetCompatibility = 1.8

tasks.compileJava.options.encoding = 'UTF-8'

repositories {
    jcenter()
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.14.4-R0.1-SNAPSHOT'
    compileOnly 'net.md-5:bungeecord-chat:1.14-SNAPSHOT'
    compile 'org.bstats:bstats-bukkit-lite:1.5'
    compile 'com.github.Leomixer17.PluginLib:bukkit:-SNAPSHOT'
}

tasks.processResources {
    filter ReplaceTokens, tokens: [
            "version"    : project.property("version"),
            "description": project.property("description")
    ]
}

tasks.jar.enabled = false

tasks.shadowJar {
    archiveFileName = 'SignsElevators.jar'
    minimize()
    relocate 'net.leomixer17.pluginlib', 'net.leomixer17.signselevators'
    relocate 'org.bstats.bukkit', 'net.leomixer17.signselevators.bstats'
}

artifacts {
    archives shadowJar
}

configurations.all {
    transitive = false
}
